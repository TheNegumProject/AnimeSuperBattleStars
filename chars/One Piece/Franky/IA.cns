;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Run]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = ifelse(pos y >= 0,60,65)

[State 0, Run Atras]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = 70

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

[State 0, Cola Charge]
type = ChangeState
Triggerall = life < Lifemax
triggerall = numhelper(570) = 0
triggerall = var(2) < 3
triggerall = power >= 500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x >= 150
trigger1 = life < lifemax/2 && random < (ailevel * 100)/8
trigger2 = life < lifemax/3 && random < (ailevel * 100)/4
trigger3 = life < lifemax/4 && random < (ailevel * 100)/2
trigger4 = life < lifemax/6 && random < (ailevel * 100)
value = 560

[State 0, Power Charge]
type = ChangeState
triggerall = statetype != A
triggerall = power < 3000
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/4
value = 500

[State 0, Power Charge End]
type = ChangeState
triggerall = time >= 12
triggerall = stateno = 500
triggerall = ailevel > 0
trigger1 = (p2bodydist x < 50) || (enemy,numproj > 0)
trigger2 = power = 3000
value = 501

[State 0, A]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 200

[State 0, Franky Triangle Jacker]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/8
value = 250

[State 0, B]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 300

[State 0, Strong Hitter]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [60,120]
trigger1 = random < (ailevel * 100)/8
value = 350

[State 0, C]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [30,60]
trigger1 = random < (ailevel * 100)/4
value = 400

[State 0, Fresh Fire]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [30,60]
trigger1 = random < (ailevel * 100)/10
value = 450

[State 0, A Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 600

[State 0, B Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 610

[State 0, C Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [30,60]
trigger1 = random < (ailevel * 100)/4
value = 620

[State 0, Master Nail]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
trigger1 = p2bodydist x = [30,60]
trigger1 = p2bodydist y > 20
trigger1 = random < (ailevel * 100)/6
trigger2 = p2bodydist x = [40,80]
trigger2 = p2bodydist y > 60
trigger2 = random < (ailevel * 100)/12
value = 650

[State 0, Franky Boxing]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)/4
value = 1000

[State 0, Coup De Poo]
type = ChangeState
triggerall = power >= 500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
trigger1 = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/8
trigger1 = p2bodydist y > -30
trigger2 = p2bodydist x = [50,100]
trigger2 = random < (ailevel * 100)/2
trigger2 = p2bodydist y <= -30
value = 1100

[State 0, Franky Destroy Hou]
type = ChangeState
triggerall = power >= 1500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [100,200]
trigger1 = random < (ailevel * 100)
value = 1200

[State 0, Ultimate Hammer]
type = ChangeState
triggerall = power >= 1500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [30,60]
trigger1 = random < (ailevel * 100)
value = 1300

[State 0, Weapons Left (Ground)]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [40,80]
trigger1 = random < (ailevel * 100)/2
value = 1400

[State 0, Weapons Left (Air)]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [30,60]
triggerall = p2bodydist y > 0
trigger1 = random < (ailevel * 100)/2
value = 1480

[State 0, Heavy Nunchaku]
type = ChangeState
triggerall = power >= 2000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)*1.25
value = 1500

[State 0, Coup De Vent]
type = ChangeState
triggerall = power >= 3000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)*2
value = 3002
;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]
[State 0, Helper de Conexiones]
type = Helper
Triggerall = numhelper(700) = 0
trigger1 = (stateno = 200||stateno = 210||stateno = 220||stateno = 600) && movecontact
trigger2 = (stateno = 300||stateno = 610) && movecontact
trigger3 = (stateno = 400||stateno = 620) && movecontact
trigger4 = stateno = 250 && time > 9 && numhelper(255) = 0
trigger5 = stateno = 352 && time > 21
trigger6 = stateno = 450 && time > 60
trigger7 = stateno = 1301 && time >= 12
helpertype = normal
name = "Helper de Conexiones"
ID = 700
stateno = 700
pos = 999999,999999
postype = p1

[State 0, HELPER ESCALA]
type = Helper
trigger1 = NUMHELPER(999999) = 0
helpertype = normal
name = "HELPER ESCALA"
ID = 999999
stateno = 999999
pos = 9999,9999
postype = P1
facing = 1
keyctrl = 0
ownpal = 1
supermove = 999999
pausemove = 999999
supermovetime = 999999
pausemovetime = 999999
;===============================================================================
; Limitador Air Jump:
;-------------------------------------------------------------------------------
[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 0 && (stateno = 65||stateno = 70)
v = 5
value = 1

[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 1 && pos y >= 0
v = 5
value = 0
;===============================================================================
; Pared Rota // Polvo en P2:
;-------------------------------------------------------------------------------
[State 0, Pared Rota]
type = Helper
triggerall = p2movetype = H
triggerall = numhelper(1070) = 0
trigger1 = p2stateno = 1091
helpertype = normal
name = "Pared Rota"
ID = 1070
stateno = 1070
pos = 0,-30
postype = p2
facing = 1
keyctrl = 0
ownpal = 1
;-------------------------------------------------------------------------------
[State 0, Polvo en P2]
type = Helper
triggerall = p2movetype = H
triggerall = numhelper(1075) = 0
trigger1 = p2stateno = 1090
helpertype = normal
name = "Polvo en P2"
ID = 1075
stateno = 1075
pos = 0,0
postype = p1
facing = 1
keyctrl = 0
ownpal = 1
;-------------------------------------------------------------------------------
; Sonidos Comunes:
;-------------------------------------------------------------------------------
[State 0, Andando // Corriendo]
type = PlaySnd
triggerall = stateno = 20 || stateno = 60
trigger1 = animelem = 3 || animelem = 7
value = S20,1
volume = 50

[State 0, Saltando]
type = PlaySnd
triggerall = stateno = 40
trigger1 = Time = 1
value = S40,1
volume = 55
channel = -1

[State 0, Super Jump]
type = PlaySnd
triggerall = stateno = 80
trigger1 = Time = 1
value = S60,1
volume = 55
channel = -1

[State 0, Land]
type = PlaySnd
triggerall = stateno = 52
trigger1 = time = 1
value = S52,1
volume = 50

[State 0, Run FWD]
type = PlaySnd
triggerall = stateno = 60
trigger1 = time = 1
value = S60,1
volume = 50

[State 0, Run FWD Air y BACK]
type = PlaySnd
triggerall = stateno = 65 || stateno = 70
trigger1 = time = 1
value = S40,1
volume = 50

[State 0, RemoveExplod]
type = RemoveExplod
TRIGGERALL = stateno != 500
trigger1 = time = 1
id = 550

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 1

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 2
[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 3

[State 0, Golpeado]
type = PlaySnd
trigger1 = movetype=H && (stateno!=[0,155]) && !time && HitOver=0 && random%3=0
value = 5000,ifelse(random < 500,1,ifelse(random < 500,2,3))

[State 0, Solucion Stand Aire]
type = ChangeState
triggerall = stateno = 0||stateno = 20
trigger1 = pos y < 0
value = 50
ctrl = 1
