;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Run]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = ifelse(pos y >= 0,60,65)

[State 0, Run Atras]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = 70

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

[State 0, Power Charge]
type = ChangeState
triggerall = statetype != A
triggerall = power < 3000
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/4
value = 500

[State 0, Power Charge End]
type = ChangeState
triggerall = time >= 12
triggerall = stateno = 500
triggerall = ailevel > 0
trigger1 = (p2bodydist x < 50) || (enemy,numproj > 0)
trigger2 = power = 3000
value = 501

[State 0, A]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 200

[State 0, B]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 300

[State 0, C]
type = ChangeState
triggerall = numhelper(450) = 0
triggerall = numhelper(451) = 0
triggerall = numhelper(70761) = 0
Triggerall = power >= 500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 50
trigger1 = random < (ailevel * 100)/8
value = 400

[State 0, A Aire]
type = ChangeState
Triggerall = pos y < -60
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [30,90]
trigger1 = random < (ailevel * 100)
value = 600

[State 0, B Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 610

[State 0, C Aire]
type = ChangeState
triggerall = p2bodydist y > 30
triggerall = numhelper(450) = 0
triggerall = numhelper(451) = 0
triggerall = numhelper(70761) = 0
triggerall = power >= 500
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [80,160]
trigger1 = random < (ailevel * 100)
value = 620

[State 0, Ice Barrage]
type = ChangeState
triggerall = numhelper(1050) = 0
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 50 || p2bodydist x < -50
trigger1 = random < (ailevel * 100)
value = 1000

[State 0, Freeze]
type = ChangeState
triggerall = numhelper(2950) = 0
triggerall = numhelper(1150) = 0
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
trigger1 = random < (ailevel * 100)
trigger1 = p2bodydist x = [45,120]
trigger2 = random < (ailevel * 100)/8
value = 1100

[State 0, Kholodnyi Smerch]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/2
value = 1200

[State 0, Aurora Thunder Attack]
type = ChangeState
triggerall = power >= 1500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x >= 60
trigger1 = random < (ailevel * 100)/4
value = 1300

[State 0, Diamound Dust]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [45,95]
trigger1 = random < (ailevel * 100)
value = 1400

[State 0, Golden freezing Coffin]
type = ChangeState
triggerall = power >= 2000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 40
trigger1 = random < (ailevel * 100)
value = 1500

[State 0, Golden Aquarius Cloth Aurora Extinction]
type = ChangeState
triggerall = power >= 3000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 200
trigger1 = random < (ailevel * 100)*2
value = 3000

; intro
[State 0, StopSnd]
type = StopSnd
trigger1 = prevstateno = 190 && time < 5
channel = 3
[State 0, StopSnd]
type = StopSnd
trigger1 = prevstateno = 190 && time < 5
channel = 4
[State 0, StopSnd]
type = StopSnd
trigger1 = prevstateno = 190 && time < 5
channel = 5

; Flame Rush
[State 0, RemoveExplod]
type = RemoveExplod
trigger1 = prevstateno = 600 && time < 5
trigger2 = stateno != 600 && numexplod(1250) > 0
id = 602
supermovetime = 999
pausemovetime = 999
supermove = 999
pausemove = 999

;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]
[State 0, Helper de Conexiones]
type = Helper
Triggerall = numhelper(700) = 0
trigger1 = stateno = 200 && movecontact
trigger2 = stateno = 210 && movecontact
trigger3 = stateno = 300 && movecontact
helpertype = normal
name = "Helper de Conexiones"
ID = 700
stateno = 700
pos = 999999,999999
postype = p1

[State 0, HELPER ESCALA]
type = Helper
trigger1 = NUMHELPER(999999) = 0
helpertype = normal
name = "HELPER ESCALA"
ID = 999999
stateno = 999999
pos = 9999,9999
postype = P1
facing = 1
keyctrl = 0
ownpal = 1
supermove = 999999
pausemove = 999999
supermovetime = 999999
pausemovetime = 999999

; Flame Rush Freno
[State 0, VelSet]
type = VelSet
trigger1 = prevstateno = 1200 && stateno = 12 && time = 1
x = 2
;===============================================================================
; Pared Rota:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = numhelper(70761) = 0
trigger1 = p2stateno = 1505
helpertype = normal
name = "Pared Rota"
ID = 70761
stateno = 70761
pos = 0,-30
postype = p2
facing = 1
keyctrl = 0
ownpal = 1

[State 0, Helper]
type = Helper
triggerall = numhelper(70762) = 0
trigger1 = p2stateno = 1506
helpertype = normal
name = "Pared Rota"
ID = 70762
stateno = 70762
pos = 0,40
postype = p2
facing = 1
keyctrl = 0
ownpal = 1
size.xscale = .4
size.yscale = .4

; Pared Rota:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = numhelper(1050) = 0
trigger1 = p2stateno = 1006
helpertype = normal
name = "Pared Rota"
ID = 1050
stateno = 10500
pos = 0,-30
postype = p2
facing = 1
keyctrl = 0
ownpal = 1
;===============================================================================
; Polvo en P2:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = p2movetype = H
triggerall = enemy,pos y >= 0
triggerall = numhelper(1055) = 0
trigger1 = p2stateno = 1005
helpertype = normal
name = "Polvo en P2"
ID = 1055
stateno = 10550
pos = 0,0
postype = p1
facing = 1
keyctrl = 0
ownpal = 1

;-------------------------------------------------------------------------------

[State 0, Gotas]
type = Explod
triggerall = numexplod(7060) = 0
triggerall = stateno != [3001,3999]
triggerall = ifelse(stateno = 3000,time < 60,alive)
triggerall = stateno != 170
triggerall = stateno != 181
triggerall = stateno != 5900
triggerall = ifelse(stateno = 190,time > 150,alive)
trigger1 = timemod = 90,0
anim = 7060
ID = 7060
pos = 0,ifelse(stateno = 11,35,25-(random%10))
postype = p1
facing = ifelse(random < 500,1,-1)
vfacing = 1
bindtime = -1
removetime = -2
scale = .4,.4
sprpriority = 2
ontop = 1
ownpal = 1
supermove = 1
supermovetime 	= 999
ignorehitpause  = 1
;-------------------------------------------------------------------------------
; Limitador Air Jump:
;-------------------------------------------------------------------------------
[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 0 && (stateno = 65||stateno = 70)
v = 5
value = 1

[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 1 && pos y >= 0
v = 5
value = 0
;-------------------------------------------------------------------------------
; Sonidos Comunes:
;-------------------------------------------------------------------------------
[State 0, Saltando]
type = PlaySnd
triggerall = stateno = 40
trigger1 = Time = 1
value = S40,1
volume = 55
channel = -1

[State 0, Andando]
type = PlaySnd
triggerall = stateno = 20||stateno = 60
trigger1 = animelem = 3 || animelem = 7
value = S20,1
volume = 50

[State 0, Super Jump]
type = PlaySnd
triggerall = stateno = 80
trigger1 = Time = 1
value = S60,1
volume = 55
channel = -1

[State 0, Land]
type = PlaySnd
triggerall = stateno = 52
trigger1 = time = 1
value = S52,1
volume = 50

[State 0, Run FWD Air y BACK]
type = PlaySnd
triggerall = stateno = 65 || stateno = 70
trigger1 = time = 1
value = S40,1
volume = 50

[State 0, RemoveExplod]
type = RemoveExplod
TRIGGERALL = stateno != 500
trigger1 = time = 1
id = 550

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 1

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 2

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 3

[State 0, Golpeado]
type = PlaySnd
trigger1 = movetype=H && (stateno!=[0,155]) && !time && HitOver=0 && random%3=0
value = 5000,ifelse(random < 500,1,ifelse(random < 500,2,3))

[State 0, Solucion Stand Aire]
type = ChangeState
triggerall = stateno = 0||stateno = 20
trigger1 = pos y < 0
value = 50
ctrl = 1
