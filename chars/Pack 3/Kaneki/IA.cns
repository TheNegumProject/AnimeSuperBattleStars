;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Run]
type = ChangeState
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 66
triggerall = stateno != 70
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = ifelse(pos y >= 0,60,65)

[State 0, Run Atras]
type = ChangeState
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 66
triggerall = stateno != 70
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = ifelse(pos y >= 0,70,66)

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

[State 0, Power Charge]
type = ChangeState
triggerall = statetype != A
triggerall = power < 3000
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/4
value = 500

[State 0, Power Charge End]
type = ChangeState
triggerall = time >= 12
triggerall = stateno = 500
triggerall = ailevel > 0
trigger1 = (p2bodydist x < 50) || (enemy,numproj > 0)
trigger2 = power = 3000
value = 501

[State 0, A]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 200

[State 0, B]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 300

[State 0, C]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [40,80]
trigger1 = random < (ailevel * 100)/8
value = 400

[State 0, A Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 600

[State 0, B Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = 610

[State 0, C Aire]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 60
triggerall = p2bodydist y > 0
trigger1 = random < (ailevel * 100)/8
value = 620

[State 0, I Will Not Kill You]
type = ChangeState
triggerall = enemynear,pos y > -50
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/4
value = 1000

[State 0, Ghoul Homicide]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
trigger1 = p2bodydist x < 50
trigger1 = random < (ailevel * 100)/4
trigger1 = p2movetype = A
trigger2 = p2bodydist x < 50
trigger2 = random < (ailevel * 100)/6
trigger2 = p2movetype != A
trigger3 = p2bodydist x = [50,100]
trigger3 = random < (ailevel * 100)/8
trigger3 = p2movetype = A
value = 1100

[State 0, Kagune Smash]
type = ChangeState
triggerall = power >= 1500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [40,80]
trigger1 = random < (ailevel * 100)
value = 1200

[State 0, Kagune Overwhelm TIERRA]
type = ChangeState
triggerall = power >= 1000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [80,150]
trigger1 = random < (ailevel * 100)/2
value = 1300

[State 0, Kagune Overwhelm AIRE]
type = ChangeState
triggerall = pos y < -50
triggerall = power >= 1000
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,100]
triggerall = p2bodydist y > 0
trigger1 = random < (ailevel * 100)/2
value = 1380

[State 0, Kagune Pierce]
type = ChangeState
triggerall = power >= 500
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [80,160]
trigger1 = random < (ailevel * 100)/4
value = 1400

[State 0, Kagune Chop]
type = ChangeState
triggerall = power >= 2000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 75
trigger1 = random < (ailevel * 100)*1.25
value = 1500

[State 0, I Will Break Half of Your Bones]
type = ChangeState
triggerall = teammode != simul
Triggerall = power >= 3000
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [50,200]
trigger1 = random < (ailevel * 100)*2
value = 3000
;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]
[State 0, Helper de Conexiones]
type = Helper
Triggerall = numhelper(700) = 0
trigger1 = (stateno = 200||stateno = 210||stateno = 600) && movecontact
trigger2 = (stateno = 300||stateno = 310||stateno = 610||stateno = 611) && movecontact
trigger3 = (stateno = 400||(stateno = 620 && time > 14)) && movecontact
trigger4 = stateno = 320 && time > 20
trigger5 = stateno = 410 && time > 40
trigger6 = stateno = 1302 && time > 10
helpertype = normal
name = "Helper de Conexiones"
ID = 700
stateno = 700
pos = 999999,999999
postype = p1

[State 0, HELPER ESCALA]
type = Helper
trigger1 = NUMHELPER(999999) = 0
helpertype = normal
name = "HELPER ESCALA"
ID = 999999
stateno = 999999
pos = 9999,9999
postype = P1
facing = 1
keyctrl = 0
ownpal = 1
supermove = 999999
pausemove = 999999
supermovetime = 999999
pausemovetime = 999999
;===============================================================================
; Pared Rota // Polvo en P2:
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = numhelper(1070) = 0
trigger1 = p2stateno = 1006
helpertype = normal
name = "Pared Rota"
ID = 1070
stateno = 1070
pos = 0,-30
postype = p2
facing = 1
keyctrl = 0
ownpal = 1
;-------------------------------------------------------------------------------
[State 0, Helper]
type = Helper
triggerall = numhelper(1075) = 0
trigger1 = p2stateno = 1005
helpertype = normal
name = "Polvo en P2"
ID = 1075
stateno = 1075
pos = 0,0
postype = p1
facing = 1
keyctrl = 0
ownpal = 1
;-------------------------------------------------------------------------------
; Sonidos Comunes:
;-------------------------------------------------------------------------------
[State 0, Andando]
type = PlaySnd
triggerall = stateno = 20 || stateno = 60
trigger1 = animelem = 3 || animelem = 6
value = S20,1
volume = 50

[State 0, Saltando]
type = PlaySnd
triggerall = stateno = 40
trigger1 = Time = 1
value = S40,1
volume = 55
channel = -1

[State 0, Super Jump]
type = PlaySnd
triggerall = stateno = 80
trigger1 = Time = 1
value = S60,1
volume = 55
channel = -1

[State 0, Land]
type = PlaySnd
triggerall = stateno = 52
trigger1 = time = 1
value = S52,1
volume = 50

[State 0, Run FWD]
type = PlaySnd
triggerall = stateno = 60
trigger1 = time = 1
value = S60,1
volume = 50

[State 0, Run FWD Air y BACK Air]
type = PlaySnd
triggerall = stateno = 65 || stateno = 66
trigger1 = time = 1
value = S40,3
volume = 50

[State 0, Run BACK]
type = PlaySnd
triggerall = stateno = 70
trigger1 = time = 1
value = S40,1
volume = 50

[State 0, RemoveExplod]
type = RemoveExplod
TRIGGERALL = stateno != 500
trigger1 = time = 1
id = 550

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 1

[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 2
[State 0, StopSnd]
type = StopSnd
TRIGGERALL = stateno != 500
trigger1 = time = 1
channel = 3

[State 0, Golpeado]
type = PlaySnd
trigger1 = movetype=H && (stateno!=[0,155]) && !time && HitOver=0 && random%3=0
value = 5000,ifelse(random < 500,1,ifelse(random < 500,2,3))

[State 0, Solucion Stand Aire]
type = ChangeState
triggerall = stateno = 0||stateno = 20
trigger1 = pos y < 0
value = 50
ctrl = 1
